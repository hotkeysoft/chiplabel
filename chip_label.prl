#!/usr/bin/perl
use strict;
use warnings;

use GD;
use Getopt::Long qw/:config auto_help gnu_getopt bundling/;
use Pod::Usage;
use List::Util qw/max min/;
use Data::Dumper;
use feature qw(switch);

use GdUtil qw/:all/;
use PTouch qw/pixels PIX_PER_MM/;
use YAML;

my $outfile;

# tape width in mm
my $tapewidth = 6;
my $pin_spacing = 2.54; # mm
my $offset = 1.0; # mm
my $force = 0;
my $all = 0;
my $chip;
my $tech;

GetOptions(
    "w=n" => \$tapewidth,  # width of tape
    "o=s" => \$outfile,
    "force" => \$force,
    "c=s" => \$chip,
    "a" => \$all,
    "t" => \$tech,
    ) or die "invalid options";

my $yaml = YAML::LoadFile("chips.yaml") or die "couldn't read chips.yaml";
my $height = pixels($tapewidth);

sub genchip {
    my ($ch) = @_;
    my @pins;
    my $name = $ch;
    if (!exists $yaml->{$ch}) {
        # attempt to generate appropriate one.
        if ($ch =~ /^[57]4[a-z]{1,3}(.*)$/) {
            my $p = $1;
#            printf "$ch $p $1\n";
            if ($p =~ /^4[0-9]{3}$/) {
                if (exists $yaml->{"CD$p"}) {
                    $ch = "CD$p";
                    @pins = @{$yaml->{$ch}{pins}};
                    map { s/^Vss$/GND/ } @pins;
                    map { s/^Vdd$/Vcc/ } @pins;
                }
            } else {
                if (exists $yaml->{"74$p"}) {
                    $ch = "74$p";
                    @pins = @{$yaml->{$ch}{pins}};
                }
            }
        }
    } else {
        @pins = @{$yaml->{$ch}{pins}};
        $name = $ch;
    }
    $name = "$name $yaml->{$ch}{name}" if exists $yaml->{$ch}{name};
    @pins or die "no pins!";
    return if defined $yaml->{$ch}{status} &&
        $yaml->{$ch}{status} eq 'disable';

    my $canvas = GD::Image->new(PIX_PER_MM*($offset*2 + $pin_spacing * (@pins/2 - 1)), $height);
    $canvas->useFontConfig(1);
    my $bg = $canvas->colorAllocate(255,255,255);
    my $fg = $canvas->colorAllocate(0,0,0);

# pin 0
    $canvas->filledRectangle(0,$height / 2 - 3, 2, $height / 2 + 3,$fg);
    $canvas->filledEllipse(2,$height / 2,7,7,$fg);

    my $nc = drawtext2($name, font => GD::Font->Tiny);
    my ($tw,$th) = $nc->getBounds();
    $canvas->copy($nc,11,($height - $th) / 2,0,0,$tw,$th);

    my $cx = $offset * PIX_PER_MM;
    for my $pl (1 .. @pins / 2) {
        my $pr = @pins - $pl + 1;

        sub dpin {
            my ($cx,$canvas,$pn,$lr) = @_;
            my $bar = $pn =~ s/^\///;
            my $t = drawtext2($pn, font => GD::Font->Tiny, overbar => $bar );
            $t = $t->copyRotate270();
            my ($tw,$th) = $t->getBounds();
            $canvas->copy($t,$cx - $tw / 2 ,$lr ? ($height - $th) : 0,0,0,$tw,$th);
        }

        dpin $cx,$canvas,$pins[$pl - 1],1;
        dpin $cx,$canvas,$pins[$pr - 1],0;

        $cx += $pin_spacing * PIX_PER_MM;
    }

    writepng($canvas , lc "out/$name.png");
}

genchip($chip) if $chip;
if ($all) {
    genchip($_) foreach keys %$yaml;
}

__END__

=head1 SYNOPSIS

 Options:
   --help            brief help message
   -w n              specify tape width in mm
   -c chip           chip name as specified in chips.yaml
   -a                generate pngs for all chips in the file
   -t (hc,ttl,ac,cd) technology of 74 or 4000 series logic

output placed in out/ directory.
